#define BLYNK_TEMPLATE_ID "TMPL69m1Osz8W"
#define BLYNK_TEMPLATE_NAME "led control"
#define BLYNK_AUTH_TOKEN "Xr-4b-3eZu5RED5YzELqDnw56hFj86a7"

#include <WiFi.h>
#include <PubSubClient.h>
#include <DHT.h>
#include <ArduinoJson.h>
#include <HTTPClient.h> 
#include "time.h"
#include <BlynkSimpleEsp32.h>

const char* ssid = "mangtrau";
const char* password = "12345678";
const char* mqtt_server = "192.168.45.22"; 
const int mqtt_port = 1883;
const char* ntfy_topic = "esp32-alerts"; 

#define DHTPIN 4       
#define DHTTYPE DHT22
#define LED_PIN 2

DHT dht(DHTPIN, DHTTYPE);
WiFiClient espClient;
PubSubClient client(espClient);
BlynkTimer timer;

BLYNK_WRITE(V0) {
  int p = param.asInt(); 
  digitalWrite(LED_PIN, p);
  Serial.print("Blynk ra lenh LED: ");
  Serial.println(p == 1 ? "ON" : "OFF");
}

String getTime() {
  struct tm timeinfo;
  if(!getLocalTime(&timeinfo)) return "00:00:00";
  char timeStringBuff[50];
  strftime(timeStringBuff, sizeof(timeStringBuff), "%H:%M:%S", &timeinfo);
  return String(timeStringBuff);
}

void sendNtfy(float t, float h, String timeStr) {
  if(WiFi.status() == WL_CONNECTED){
    HTTPClient http;
    String url = "https://ntfy.sh/" + String(ntfy_topic);
    http.begin(url);
    
    String message = "Notification! \nTemperature: " + String(t) + "C \nHumidity: " + String(h);
    
    http.addHeader("Title", "ESP32 Sensor Update");
    http.addHeader("Priority", "5"); 
    
    int httpResponseCode = http.POST(message);
    if (httpResponseCode > 0) Serial.println("Notification sent via ntfy!");
    http.end();
  }
}

void setup_wifi() {
  WiFi.begin(ssid, password);
  while (WiFi.status() != WL_CONNECTED) { delay(500); Serial.print("."); }
  Serial.println("\nWiFi Connected!");
  configTime(25200, 0, "pool.ntp.org"); 
}

void reconnect() {
  while (!client.connected()) {
    String clientId = "ESP32-" + String(WiFi.macAddress());
    if (client.connect(clientId.c_str())) Serial.println("MQTT Connected!");
    else delay(5000);
  }
}

void setup() {
  Serial.begin(115200);
  pinMode(LED_PIN, OUTPUT);

  Blynk.begin(BLYNK_AUTH_TOKEN, ssid, password);

  dht.begin();
  setup_wifi();
  client.setServer(mqtt_server, mqtt_port);
}

void loop() {
  Blynk.run();

  if (!client.connected()) reconnect();
  client.loop();

  static unsigned long lastMsg = 0;
  static unsigned long lastNotify = 0;
  unsigned long now = millis();
  
  if (now - lastMsg > 3000) {
    lastMsg = now;
    float h = dht.readHumidity();
    float t = dht.readTemperature();
    String currentTime = getTime();

    if (isnan(h) || isnan(t)) return;

    JsonDocument doc; 
    doc["temp"] = t; 
    doc["hum"] = h;
    
    char buffer[256];
    serializeJson(doc, buffer);
    client.publish("iot/data", buffer);

    if (now - lastNotify > 100000) {
       sendNtfy(t, h, currentTime);
       lastNotify = now;
    }
  }
}